start: (NEWLINE|statement|funcdef)+ ;
identifier: NAME;
statement: assign|var|vardec|expr|ret|func|_if|_while|_print ;
funcdef: (identifier) '\(' (vardec)* '\)' RET (ftype) (block) ;
assign: (vardec|var) '=' (expr)  ;

vardec: INT (identifier)  ;
var: (identifier) ;
expr: (var|number|add|sub|mul|mod|_and|_or|_xor|invert|div|func|gt|lt|eq|le|ge|ne) (expr)* ;
add: (var|number) '\+' (var|number) ;
sub: (var|number) '\-' (var|number) ;
mul: (var|number) '\*' (var|number) ;
div: (var|number) '\/' (var|number) ;
mod: (var|number) '%' (var|number) ;
_and: (var|number) '&' (var|number) ;
_or: (var|number) '\|' (var|number) ;
_xor: (var|number) '\^' (var|number) ;
invert: '\~' (var|number) ;
gt: (var|number) '>' (var|number) ;
block: '{' (statement|NEWLINE)+ '}' ;
ret: RETURN (expr) ;
func: (identifier) '\(' '\)' ;

ftype: 'void' | INT ;
number: '[0-9]+' ;
SPACES: '[ \t;]+' (%ignore) ;
NEWLINE: '(\r?\n[\t ]*)+' (%newline) ;
_print: PRINT (var) ;
lt: (number|var) '<' (number|var) ;
eq: (number|var) '==' (number|var) ;
le: (number|var) '<=' (number|var) ;
ge: (number|var) '>=' (number|var) ;
ne: (number|var) '!=' (number|var) ;
_if: IF '\(' (expr) '\)' (block); 
_while: WHILE '\(' (expr) '\)' (block) ;
NAME: '[a-zA-Z_][a-zA-Z_0-9]*' (%unless 
INT: 'int' ;
VOID: 'void' ;
IF: 'if';
RET: '->';
RETURN: 'return' ;
WHILE: 'while' ;
PRINT: 'print' ;
) ;
